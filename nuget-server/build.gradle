plugins {
    id 'io.github.rodm.teamcity-server' version '1.5.2'
}

teamcity {
    version = teamcityVersion
    allowSnapshotVersions = true

    server {
        descriptor = project.file('teamcity-server-plugin.xml')
        tokens = [Plugin_Version: project.version]

        files {
            into('bin') {
                from (project(':nuget-extensions').file('bin')) {
                    include '**/*JetBrains*'
                    include '**/*TeamCity*'
                    include '**/*Newtonsoft*.dll'
                    include '**/*PowerArgs*.dll'
                    include '**/NuGet.*.dll'
                    include '**/System.ValueTuple.dll'
                    exclude '**/*.deps.json'
                    exclude '**/*.dev.json'
                }
            }

            into('licenses') {
                from (project.file('../licenses'))
            }

            into('kotlin-dsl') {
                from("${rootProject.projectDir}/kotlin-dsl")
            }
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir

        teamcity_2017_1 {
            version = '2017.1'
            downloadUrl = 'https://download.jetbrains.com/teamcity/TeamCity-2017.1.tar.gz'
        }

        teamcity_2019_1 {
            version = '2019.1'
            downloadUrl = 'https://download.jetbrains.com/teamcity/TeamCity-2019.1.tar.gz'
        }
    }
}

dependencies {
    agent project(path: ':nuget-agent', configuration: 'plugin')

    implementation project(':nuget-common')
    implementation project(':nuget-feed')
    implementation project(':nuget-server-common')

    provided 'org.apache.httpcomponents:httpcore:4.4.12'
    provided 'org.apache.httpcomponents:httpclient:4.5.10'
    provided "com.github.ben-manes.caffeine:caffeine:2.4.0"
    provided "org.jetbrains.teamcity.internal:server:$teamcityVersion"
    provided "org.jetbrains.teamcity.internal:web:$teamcityVersion"
    provided "org.jetbrains.teamcity.internal:server-tools:$teamcityVersion"
    provided "org.jetbrains.teamcity:nuget-utils:$teamcityVersion"
}

serverPlugin.version = null
serverPlugin.baseName = 'dotNetPackagesSupport'

test.dependsOn(":nuget-tests:test")
